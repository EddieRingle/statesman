buildscript {
  ext {
    aver = [
        app       : "0.3.0",
        appCode   : 2,
        buildTools: "23.0.2",
        compileSdk: 23,
        minSdk    : 16,
        plugin    : "2.1.0-alpha3",
        support   : "23.2.1",
    ]
    ver = [
        gradleMaven: "1.3",
        java       : JavaVersion.VERSION_1_7,
        kotlin     : "1.0.1",
    ]
    adep = [
        annotations: "com.android.support:support-annotations:${aver.support}",
    ]
    dep = [
        kotlinStdlib: "org.jetbrains.kotlin:kotlin-stdlib:${ver.kotlin}",
    ]
    plug = [
        android    : "com.android.tools.build:gradle:${aver.plugin}",
        kotlin     : "org.jetbrains.kotlin:kotlin-gradle-plugin:${ver.kotlin}",
        mavenGradle: "com.github.dcendents:android-maven-gradle-plugin:${ver.gradleMaven}",
    ]
    commonRepos = {
      jcenter()
    }
  }
  repositories.configure commonRepos
  dependencies {
    plug.each { n, p ->
      classpath p
    }
  }
}

subprojects {
  beforeEvaluate { p ->
    def androidDefaults = {
      p.android {
        buildToolsVersion aver.buildTools
        compileSdkVersion aver.compileSdk

        defaultConfig {
          minSdkVersion aver.minSdk
          targetSdkVersion aver.compileSdk
          versionCode aver.appCode
          versionName aver.app
        }

        compileOptions {
          sourceCompatibility ver.java
          targetCompatibility ver.java
        }
      }
    }
    plugins.withId("com.android.application", androidDefaults)
    plugins.withId("com.android.library", androidDefaults)
  }
  repositories.configure commonRepos
}
